// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // set x and y to 0, if needed
    Mux16(a=x, b=false, sel=zx, out=zerox);
    Mux16(a=y, b=false, sel=zy, out=zeroy);

    // !x and !y
    Not16(in=zerox, out=notx);
    Not16(in=zeroy, out=noty);

    Mux16(a=zerox, b=notx, sel=nx, out=initx);
    Mux16(a=zeroy, b=noty, sel=ny, out=inity);

    // chose between x + y and x & y based on f
    // if (f == 1)  sets out = x + y, else out = x & y
    And16(a=initx, b=inity, out=xandy);
    Add16(a=initx, b=inity, out=xplusy);

    Mux16(a=xandy, b=xplusy, sel=f, out=fout);

    // set out and ng
    // ng - we can use the fact that most negative numbers will start with 1
    Not16(in=fout, out=notf);
    // syntax below comes from here[1].
    Mux16(a=fout, b=notf, sel=no, out[0..7]=p1, out[8..15]=p2, out[15]=ng, out=out);

    // zr - if (out == 0) equals 1, else 0
    Or8Way(in=p1, out=or8way1);  // this will be 0 if all value is 0
    Or8Way(in=p2, out=or8way2);

    Or(a=or8way1, b=or8way2, out=zrtmp);

    Not(in=zrtmp, out=zr); // invert result
}


// [1] https://www.reddit.com/r/NandToTetris/comments/pzlf93/comment/husku2y/
